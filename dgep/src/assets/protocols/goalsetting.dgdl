System{GoalSetting{
	/* Description: A game to discuss a physical activity goal */

	turns{magnitude:multiple, ordering:liberal}
	roles{Person}
	players{min:2, max:3}
	player{id:Agent, roles{Person}, max:2, min:1}
	player{id:User, roles{Person}, max:1, min:1}

	backtrack{on}

  rule{id:StartingRule, scope:initial,
    {
        assign(Agent, speaker)
        & move(add, next, Propose, $User, {p}, Agent)
    }
  }

  interaction{Propose, $User, {p}, "$p",
    {
      assign(User, speaker)
      & move(add, next, AcceptGoal, $Agent, {p}, User)
      & move(add, next, ChallengeGoal, $Agent, {p}, User)
      & move(add, next, RejectGoalTooHigh, $Agent, {p}, User)
			& move(add, next, RejectGoalTooLow, $Agent, {p}, User)
			& save({p}, $AgreedGoal$)
    }
  }

  interaction{AcceptGoal, $Agent, {p}, "$p",
    {
      assign(Agent, speaker)
			& move(add, next, GoalAccepted, $User, {p}, Agent)
    }
  }

	interaction{ChallengeGoal, $Agent, {p}, "$p",
		{
			assign(Agent, speaker)
			& move(add, next, JustifyGoal, $User, {p,q}, Agent)
		}
	}

	interaction{JustifyGoal, $User, {p,q}, "$q",
		{
			assign(User, speaker)
			& move(add, next, AcceptGoal, $Agent, {p}, User)
			& move(add, next, RejectGoalTooHigh, $Agent, {p}, User)
			& move(add, next, RejectGoalTooLow, $Agent, {p}, User)
		}
	}

	interaction{RejectGoalTooHigh, $Agent, {p}, "$p",
		{
			assign(Agent, speaker)
			& move(add, next, Propose, $User, {q}, Agent)
			& move(add, next, Breakout, $User, {p}, Agent)
		}
	}

	interaction{RejectGoalTooLow, $Agent, {p}, "$p",
		{
			assign(Agent, speaker)
			& move(add, next, Propose, $User, {q}, Agent)
			& move(add, next, Breakout, $User, {p}, Agent)
		}
	}

	interaction{Breakout, $User, {p}, "$p",
		{
			move(add, next, End, $User, {$AgreedGoal$}, Agent)
		}

	}

	interaction{GoalAccepted, $User, {p}, "$p",
		{
			move(add, next, End, $User, {p}, Agent)
		}

	}

	interaction{End, $User, {p}, "$p",
		{
			status(terminate, GoalSettingMain)
		}
	}
}}
